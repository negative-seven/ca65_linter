use std::str::FromStr;
use crate::ast::*;

grammar;

match {
    r"[ \t\r]+" => { },
} else {
    _
}

Identifier: Identifier = <start:@L> <s:r"@?[a-zA-Z][a-zA-Z0-9_]*"> <end:@R> => Identifier {
    string: s.to_string(),
    span: Span {
        start,
        end,
    },
};

NumberLiteral: NumberLiteral = {
    <start:@L> <digits:r"\$[0-9a-fA-F]+"> <end:@R> => NumberLiteral {
        value: u16::from_str_radix(&digits[1..], 16).unwrap(),
        base: NumberLiteralBase::Hexadecimal,
        span: Span {
            start,
            end,
        },
    },
    <start:@L> <digits:r"[0-9]+"> <end:@R> => NumberLiteral {
        value: u16::from_str(digits).unwrap(),
        base: NumberLiteralBase::Decimal,
        span: Span {
            start,
            end,
        },
    },
}

Expression: Expression = {
    <e:Identifier> => Expression::Identifier(e),
    <e:NumberLiteral> => Expression::NumberLiteral(e),
}

ImplicitLabel: ImplicitLabel = <identifier:Identifier> ":" => ImplicitLabel {
    identifier,
};

ExplicitLabel: ExplicitLabel = <identifier:Identifier> ":=" <value:Expression> => ExplicitLabel {
    identifier,
    value,
};

InstructionOperandKind: InstructionOperandKind = {
    => InstructionOperandKind::None,
    "#" <expression:Expression> => InstructionOperandKind::Immediate(expression),
    <expression:Expression> => InstructionOperandKind::Direct(expression),
}

InstructionOperand: InstructionOperand = <start:@L> <kind:InstructionOperandKind> <end:@R> => InstructionOperand {
    kind,
    span: Span {
        start,
        end,
    },
};

Instruction: Instruction = <mnemonic:Identifier> <operand:InstructionOperand> => Instruction {
    mnemonic,
    operand,
};

Statement: Statement = {
    <s:ExplicitLabel> => Statement::ExplicitLabel(s),
    <s:ImplicitLabel> => Statement::ImplicitLabel(s),
    <s:Instruction> => Statement::Instruction(s),
}

LineStatements: Vec<Statement> = {
    r"\n+" => vec![],
    <s:ExplicitLabel> r"\n+" => vec![Statement::ExplicitLabel(s)],
    <s:ImplicitLabel> r"\n+" => vec![Statement::ImplicitLabel(s)],
    <s:Instruction> r"\n+" => vec![Statement::Instruction(s)],
}

pub Statements: Vec<Statement> = <s:LineStatements*> => s.into_iter().flatten().collect();
